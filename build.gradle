plugins {
    id 'java'
    id 'jacoco'
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
}

group 'com.truedev'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}
configurations {
    ktlint
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.apache.httpcomponents:httpclient:4.5.10"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.10.0"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.0"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.10.0"
    implementation "com.fasterxml.jackson.module:jackson-module-parameter-names:2.10.0"
    ktlint "com.pinterest:ktlint:0.35.0"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}


jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn jacocoTestReport, ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
